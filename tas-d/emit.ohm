emit {

  Main = TopLevel+
  TopLevel =
    | Defvar -- defvar
    | Defconst -- defconst
    | Defn -- defn
    | Defobj -- defobj
    | Import -- import
    | External -- external
    | comment line? -- comment
    | line -- line

   Defvar = "defvar" Lval "⇐" Exp line?
   Defconst = "defconst" Lval "≡" Exp line?
   Defn = "defn" ident Formals StatementBlock line?
   Defobj = "defobj" ident ObjFormals line? "{" line? InitStatement* "}" line?
   Import = "import" ident line?

   StatementBlock = line? "{" line? Rec_Statement line? "}" line?

   Rec_Statement = line? R_Statement line?
   R_Statement =
     | comment Rec_Statement? -- comment
     | External Rec_Statement? -- external
     | Deftemp -- deftemp
     | Defsynonym -- defsynonym     
     | "global" ident CommaIdent* Rec_Statement? -- globals
     | IfStatement  -- if
     | "pass" Rec_Statement? -- pass
     | "return" ReturnExp -- return
     | ForStatement -- for
     | WhileStatement  -- while
     | Assignment -- assignment
     | Lval Rec_Statement? -- call
     | line Rec_Statement? -- line
   CommaIdent = Comma ident

   External = ExternalPhrase line?
   ExternalPhrase =
     | "#" "low_level_read_text_file_handler" "(" eh Comma msg Comma fname Comma ok Comma err ")" -- read
     | "#" "read_and_convert_lnet_file" "(" pathname Comma fname ")" -- raclnetf
     | "#" "internalize_lnet_from_string" "(" ")" -- internalizeLnetFromString
     | "#" "freshQueue" "(" ")" -- freshQueue
     | "#" "resetQueue" "(" Exp ")" -- resetQueue
     | "#" "freshStack" "(" ")" -- freshStack
     | "#" "fresh" "(" ident ")" -- fresh
     | "#" "stringcar" "(" Exp ")" -- stringcar
     | "#" "stringcdr" "(" Exp ")" -- stringcdr
     | "#" "strcons" "(" Exp Comma Exp ")" -- strcons
     | "#" "stringshrink" "(" Exp ")" -- stringshrink
     | "#" "append" "(" Exp Comma Exp ")" -- append
     | "#" "basename" "(" Exp ")" -- basename
     | "#" "preamble" "(" ")" -- preamble
     | "#" "print_stdout" "(" Exp ")" -- print_stdout
     | "#" "print_stderr" "(" Exp ")" -- print_stderr
     | "#" "print_nl" "(" ")" -- print_nl
     | "#" "print_nl_stderr" "(" ")" -- print_nl_stderr
     | "#" "display_queue_as_json" "(" Exp ")" -- display_queue_as_json
     | "#" "substitute" "(" Exp Comma Exp Comma Exp ")" -- substitute
     | "#" "run_command" "(" Cmd Comma Args Comma ret Comma rc Comma stdout Comma stderr ")" -- run_command
     | "#" "abort" "(" ")" -- abort
     | "#" "asstr" "(" Exp ")" -- asstr
     | "#" "len" "(" Exp ")" -- len
     | "#" "asint" "(" Exp ")" -- asint
     | "#" "enqueue" "(" Exp Comma Exp ")" -- enqueue
     | "#" "dequeue" "(" Exp ")" -- dequeue
     | "#" "empty" "(" Exp ")" -- empty
     | "#" "queue2list" "(" Exp ")" -- queue2list
     | "#" "negate" "(" Exp ")" -- negate
     | "#" "live_update" "(" Exp Comma Exp ")" -- liveUpdate
     | "#" "queue_as_json_to_stdout" "(" Exp ")" -- queueAsJsonToStdout
     | "#" "dir" "(" Exp ")" -- dir
     | "#" "dispatch_lookup" "(" Exp Comma Exp ")" -- dispatch_lookup
     | "#" "dispatch" "(" Exp Comma Exp Comma Exp")" -- dispatch
     
     | "#" ident "(" StuffInsideParentheses* ")" -- unrecognized


   Cmd = Exp
   Args = Exp
   ret = ident
   rc = ident
   stdout = ident
   stderr = ident
   
   Deftemp = "deftemp" Lval "⇐" Exp Rec_Statement?
   Defsynonym =
     | Lval errorMessage "≡" Exp Rec_Statement? -- illegal
     | ident "≡" Exp Rec_Statement? -- legal

   InitStatement = "•" ident "⇐" Exp (comment | line)*

   IfStatement = "if" Exp StatementBlock ElifStatement* ElseStatement? Rec_Statement?
   ElifStatement = "elif" Exp StatementBlock
   ElseStatement = "else" StatementBlock

   ForStatement = "for" ident "in" Exp StatementBlock Rec_Statement?
   WhileStatement = "while" Exp StatementBlock Rec_Statement?

   Assignment = 
     | "[" LvalComma+ "]" "⇐" Exp Rec_Statement? -- multiple
     | Lval "⇐" Exp Rec_Statement? -- single

   LvalComma = Lval Comma?

    ReturnExp =
      | "[" ExpComma+ "]" Rec_Statement? -- multiple
      | Exp Rec_Statement? -- single

    ExpComma = Exp Comma?
    
    Exp =  BooleanAndOrIn

    BooleanAndOrIn =
      | BooleanAndOrIn andOrIn BooleanExp -- andOrIn
      | BooleanExp -- default
      
    BooleanExp =
      | BooleanExp boolNeq BooleanNot -- boolopneq
      | BooleanExp boolOp BooleanNot -- boolop
      | BooleanNot -- basic

    BooleanNot =
      | "not" BooleanExp -- not
      | AddExp -- basic

    AddExp =
      | AddExp "+" MulExp  -- plus
      | AddExp "-" MulExp  -- minus
      | MulExp -- basic

    MulExp =
      | MulExp "*" ExpExp  -- times
      | MulExp "/" ExpExp  -- divide
      | ExpExp -- basic

    ExpExp =
      | Primary "^" ExpExp  -- power
      | Primary -- basic

    Primary =
      | PrimaryIndexed Actuals -- call
      | PrimaryIndexed -- plain

    PrimaryIndexed =
      | PrimaryIndexed "@" ident -- lookupident
      | PrimaryIndexed "@" PrimaryIndexed -- lookup
      | PrimaryIndexed "." ident -- fieldident
      | PrimaryIndexed "." PrimaryIndexed -- field
      | PrimaryIndexed "[" Exp "]" -- index
      | PrimaryIndexed "[" digit+ ":" "]" -- nthslice
      | Atom -- atom

    Atom =
      | External -- external
      | "[" "]" -- emptylistconst
      | "{" "}" -- emptydict
      | "(" Exp ")" -- paren
      | "[" line? PrimaryComma+ line? "]" -- listconst
      | "{" line? PairComma+ line? "}" -- dict
      | lambda LambdaFormals? ":" Exp -- lambda
      | phi -- phi
      | "⊤" -- true
      | "⊥" -- false
      | "↪︎" ident -- subr
      | "range" "(" Exp ")" -- range
      | ident Actuals -- callident
      | string -- string
      | number -- number
      | ident -- ident



    PrimaryComma = Primary Comma? line?
    PairComma = Pair Comma?

    StuffInsideParentheses =
      | "(" StuffInsideParentheses* ")" -- rec
      | ~"(" ~")" any -- default
    
    Lval = Exp

    Formals =
      | "(" ")" -- noformals
      | "(" FormalComma* ")" -- withformals
    ObjFormals =
      | "(" ")" -- noformals
      | "(" FormalComma* ")" -- withformals
    LambdaFormals =
      | "(" ")" -- noformals
      | "(" FormalComma* ")" -- withformals

    Formal = 
       | ident -- plain
       
    FormalComma = Formal Comma?
    
    Actuals = 
      | "(" ")" -- noactuals
      | "(" ActualComma* ")" line? -- actuals

   Actual = Exp
   ActualComma = comment? Actual Comma? line?

    number =
      | digit* "." digit+  -- fract
      | digit+             -- whole

    Pair = string ":" Exp Comma?
  

  andOrIn =
    | "and" -- and
    | "or" -- or
    | "in" -- in

  boolOp = (boolEq | boolNeq | "<=" | ">=" | "=" | ">" | "<")
  boolEq = "="
  boolNeq = "!="

  string = "“" stringchar* "”"
  stringchar = 
    | "“" stringchar* "”" -- rec
    | ~"“" ~"”" any -- other

    keyword = (
        "defconst"
      | "deftemp"
      | "defobj"
      | "defvar"
      | "defn"
      | "useglobal"
      | "pass"
      | "return"
      | "if"
      | "elif"
      | "else"
      | "and"
      | "or"
      | "in"
      | "not"
      | "range"
      | "while"
      | "import"
      | "as"
      | "pair"
      | "push"
      | lambda
      | phi
      )
      
  lambda = ("λ" | "%CE%BB")
  phi = ("ϕ" | "%CF%95")

  ident  = ~keyword idchar+
  idchar = alnum | "_"

  comment = "⌈" commentchar* "⌉"
  commentchar = 
    | "⌈" commentchar* "⌉" -- rec
    | ~"⌈" ~"⌉" any -- other

  errorMessage = "⎝" errorchar* "⎠"
  errorchar =  
    | "⎝" errorchar* "⎠" -- rec
    | ~"⎝" ~"⎠" any -- other

  eh = ident
  fname = ident
  pathname = ident
  msg = ident
  ok = port
  err = port
  port = string
  Key = Exp
  Kvs = Exp

  line = "⎩" (~"⎩" ~"⎭" any)* "⎭"

  Comma = line? "," line?
}
